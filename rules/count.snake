
""" rules for scraps counting """

rule assign_sites_read2:
  input:
    "{results}/{sample}/{sample}_{read}_Aligned.sortedByCoord.out.bam"
  output:
    bam = "{results}/counts/{sample}_{read}.bam",
    bai = "{results}/counts/{sample}_{read}.bam.bai"
  params:
    job_name = "assign_sites_read2",
    memory = "select[mem>8] rusage[mem=8]", # LSF format; change as needed
    out = "{results}/counts/{sample}_{read}_assigned",
    out_bam = "{results}/counts/{sample}_{read}_Aligned.sortedByCoord.out.bam.featureCounts.bam"
  log:
    "logs/assign_sites_{sample}_{read}.txt"
  threads:
    12
  shell:
    """
    if echo {POLYA_SITES} | tr '[:upper:]' '[:lower:]' | grep -q -e "\.saf$" -e "\.saf.gz$"; then
      polyaformat='SAF'; else
      polyaformat='GTF';
    fi

    featureCounts \
    -s 1 -Q 10 -O \
    --read2pos 3 \
    -o {params.out} \
    -F $polyaformat \
    -a {POLYA_SITES} \
    -R BAM \
    -T {threads} \
    {input} \
    2> {log}
    
    samtools sort \
    {params.out_bam} \
    -o {output.bam}
    
    samtools index {output.bam}
    """
    
rule count:
  input:
    "{results}/counts/{sample}_{read}.bam"
  output:
    "{results}/counts/{sample}_{read}_counts.tsv.gz"
  params:
    job_name = "count",
    memory = "select[mem>8] rusage[mem=8]" # LSF format; change as needed
  log:
    "logs/count_{sample}_{read}.txt"
  threads:
    1
  shell:
    """
    umi_tools count \
      --per-gene \
      --gene-tag=XT \
      --assigned-status-tag=XS \
      --per-cell \
      -I {input} \
      -S {output} \
      > {log}
    """
