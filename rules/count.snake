""" Extract correction length target"""
def _get_chem_length_R1(wildcards):
    chem_version = SAMPLES_DF[SAMPLES_DF.capture == wildcards.sample].chemistry.unique()
    if chem_version == "chromiumV3":
        args = ["58"]
    elif chem_version == "chromiumV2":
        args = ["56"]
    elif chem_version == "dropseq":
        args = ["50"]
    elif chem_version == "microwellseq":
        args = ["54"]
    return args

""" rules for scraps counting """

rule assign_sites_read2:
  input:
    "{results}/temp/{sample}_R2.bam"
  output:
    bam = temp("{results}/temp/{sample}_R2_assigned.bam")
  params:
    job_name = "assign_sites_read2",
    memory = "select[mem>8] rusage[mem=8]", # LSF format; change as needed
    out = "{results}/temp/{sample}_R2_assigned",
    out_bam = "{results}/temp/{sample}_R2.bam.featureCounts.bam"
  log:
    "logs/assign_sites_{sample}_R2.txt"
  threads:
    12
  shell:
    """
    if echo {POLYA_SITES} | tr '[:upper:]' '[:lower:]' | grep -q -e "\.saf$" -e "\.saf.gz$"; then
      polyaformat='SAF'; else
      polyaformat='GTF';
    fi

    featureCounts \
    -s 1 -Q 10 -O \
    --read2pos 3 \
    -o {params.out} \
    -F $polyaformat \
    -a {POLYA_SITES} \
    -R BAM \
    -T {threads} \
    {input} \
    2> {log}
    
    samtools sort \
    {params.out_bam} \
    -o {output.bam}
    
    samtools index {output.bam}

    rm -rf {params.out_bam}
    """

rule assign_sites_read1:
  input:
    "{results}/temp/{sample}_R1.bam"
  output:
    bam = temp("{results}/temp/{sample}_R1_assigned.bam")
  params:
    job_name = "assign_sites_read1",
    memory = "select[mem>8] rusage[mem=8]", # LSF format; change as needed
    out = "{results}/temp/{sample}_R1_assigned",
    out_bam = "{results}/temp/{sample}_R1.bam.featureCounts.bam"
  log:
    "logs/assign_sites_{sample}_R1.txt"
  threads:
    12
  shell:
    """
    if echo {POLYA_SITES} | tr '[:upper:]' '[:lower:]' | grep -q -e "\.saf$" -e "\.saf.gz$"; then
      polyaformat='SAF'; else
      polyaformat='GTF';
    fi

    featureCounts \
    -s 2 -Q 10 -O \
    --read2pos 5 \
    -o {params.out} \
    -F $polyaformat \
    -a {POLYA_SITES} \
    -R BAM \
    -T {threads} \
    {input} \
    2> {log}

    samtools sort \
    {params.out_bam} \
    -o {output.bam}

    samtools index {output.bam}
    rm -rf {params.out_bam}
    """
    
rule filterR2:
  input:
    "{results}/{sample}/{sample}_R2_Aligned.sortedByCoord.out.bam"
  output:
    temp("{results}/temp/{sample}_R2.bam")
  params:
    job_name = "filter",
    temp = "{results}/temp/{sample}_R2_filter.bam",
    tempbai = "{results}/temp/{sample}_R2_filter.bam.bai",
    memory = "select[mem>8] rusage[mem=8]" # LSF format; change as needed
  log:
    "logs/filter_{sample}_R2.txt"
  threads:
    4
  shell:
    """
    samtools view -h {input} | grep -v 'CB:Z:-\|UB:Z:-' | samtools view -b - > {params.temp}
    samtools index {params.temp}
    python3 inst/scripts/filter_bam.py -i {params.temp} -o {output}
    samtools index {output}
    rm -rf {params.temp}
    rm -rf {params.tempbai}
    """

rule filterR1:
  input:
    "{results}/{sample}/{sample}_R1_Aligned.sortedByCoord.out.bam"
  output:
    temp("{results}/temp/{sample}_R1.bam")
  params:
    job_name = "filter",
    length = _get_chem_length_R1,
    temp = "{results}/temp/{sample}_R1_filter.bam",
    temp2 = "{results}/temp/{sample}_R1_filter2.bam",
    memory = "select[mem>8] rusage[mem=8]" # LSF format; change as needed
  log:
    "logs/filter_{sample}_R1.txt"
  threads:
    4
  shell:
    """
    samtools view -h {input} | grep -v 'CB:Z:-\|UB:Z:-' | samtools view -b - > {params.temp}
    python3 inst/scripts/filter_bam_correct.py -i {params.temp} -o {params.temp2} -l {params.length}
    samtools sort {params.temp2} -o {output}
    # mv {params.temp} {output}
    samtools index {output}
    rm -rf {params.temp}
    rm -rf {params.temp2}
    """
    
rule count:
  input:
    "{results}/temp/{sample}_{read}_assigned.bam"
  output:
    "{results}/counts/{sample}_{read}_counts.tsv.gz"
  params:
    job_name = "count",
    memory = "select[mem>8] rusage[mem=8]" # LSF format; change as needed
  log:
    "logs/count_{sample}_{read}.txt"
  threads:
    4
  shell:
    """
    umi_tools count \
      --per-gene \
      --gene-tag=XT \
      --assigned-status-tag=XS \
      --per-cell \
      --extract-umi-method=tag \
      --umi-tag=UB \
      --cell-tag=CB \
      -I {input} \
      -S {output} \
      > {log}
    """

rule bedR1:
  input:
    "{results}/temp/{sample}_R1.bam"
  output:
    "{results}/bed/{sample}_R1.bed.gz"
  params:
    job_name = "bed",
    dedup = "{results}/temp/{sample}_R1_dedup.bam",
    r = "{results}/temp/{sample}_R1_r.bam",
    memory = "select[mem>48] rusage[mem=48]" # LSF format; change as needed
  log:
    "logs/bed_{sample}_R1.txt"
  threads:
    4
  shell:
    """
    umi_tools dedup \
      --extract-umi-method=tag \
      --umi-tag=UB \
      --per-cell \
      --cell-tag=CB \
      --paired \
      --method=unique \
      -I {input} \
      -S {params.dedup} \
      > {log}
    # 0x82 for R2
    samtools view -f 0x42 {params.dedup} -b > {params.r}
    bedtools genomecov -ibam {params.r} -5 -dz | awk 'BEGIN {{ OFS = "\t" }} {{ print $1, $2 - 1, $2, $3 }}' - | gzip > {output}
    rm -rf {params.r}
    """

rule bedR2:
  input:
    "{results}/temp/{sample}_R2.bam"
  output:
    "{results}/bed/{sample}_R2.bed.gz"
  params:
    job_name = "bed",
    dedup = "{results}/temp/{sample}_R2_dedup.bam",
    memory = "select[mem>48] rusage[mem=48]" # LSF format; change as needed
  log:
    "logs/bed_{sample}_R2.txt"
  threads:
    4
  shell:
    """
    umi_tools dedup \
      --extract-umi-method=tag \
      --umi-tag=UB \
      --per-cell \
      --cell-tag=CB \
      --method=unique \
      -I {input} \
      -S {params.dedup} \
      > {log}

    bedtools genomecov -ibam {params.dedup} -3 -dz | awk 'BEGIN {{ OFS = "\t" }} {{ print $1, $2 - 1, $2, $3 }}' - | gzip > {output}
    """
